apply plugin: 'maven-publish'

def mavenDistrTaskGroupName = "maven"
def refBuildType = "release"
def mavenSdkPublicationTaskNamePrefix = "myTestLibraryLib"
def mavenPublicationPlaceholderTaskName = "publish${mavenSdkPublicationTaskNamePrefix.capitalize()}%sPublicationToGitHubPkgRepository"
def flavorUploadMavenPrefixTaskName = "uploadToGitHubPkg"
def uploadToGitHubTasksName = new ArrayList<String>()

publishing {
    publications {

        android.libraryVariants.all { variant ->
            if (variant.buildType.name == refBuildType) {

                def flavorName = variant.productFlavors[0].name
                def artifactVersionName = "${BUILD_MAVEN_SDK_VERSION}-${flavorName}-release"

                "${mavenSdkPublicationTaskNamePrefix}${variant.name.capitalize()}"(MavenPublication) {
                    groupId "com.pditta.githubactions"
                    artifactId "mytestlibrary"
                    version artifactVersionName
                    artifact "$buildDir/outputs/aar/myTestLibrary-${variant.name}.aar"

                    pom.withXml {

                        def dependenciesNode = asNode().appendNode('dependencies')
                        configurations.implementation.allDependencies.each {
                            println "BUILD implementation  node: ${it.group}:${it.name}:${it.version}"
                            addDependency(it, dependenciesNode)
                        }

                        configurations.getByName("${flavorName}Implementation").allDependencies.each {
                            println "BUILD ${flavorName}Implementation node: ${it.group}:${it.name}:${it.version}"
                            addDependency(it, dependenciesNode)
                        }

                    }
                }
            }
        }
    }
    repositories {
        maven {
            name = "GitHubPkg"
            url = uri("https://maven.pkg.github.com/pierreNtakka/android_github_actions_example")
            credentials {
                username = "${System.getenv("GPR_USER")}"
                password = "${System.getenv("GITHUB_TOKEN")}"
            }
        }
    }
}

static def addDependency(dependency, dependenciesNode) {
    def dependencyNode = dependenciesNode.appendNode('dependency')
    dependencyNode.appendNode('groupId', dependency.group)
    dependencyNode.appendNode('artifactId', dependency.name)
    dependencyNode.appendNode('version', dependency.version)
}

android.libraryVariants.configureEach { variant ->

    def variantName = "${variant.name.capitalize()}"
    def assembleVariantName = "assemble${variantName}"

    def unitTestTaskName = "test${variantName}UnitTest"

    if (variant.buildType.name == refBuildType) {

        def mavenPublicationTaskName = String.format(mavenPublicationPlaceholderTaskName, variantName)
        tasks.named(mavenPublicationTaskName).get().dependsOn(assembleVariantName)

        uploadToGitHubTasksName.add(mavenPublicationTaskName)

        tasks.register("${flavorUploadMavenPrefixTaskName}${variantName}") {
            group = mavenDistrTaskGroupName
            description = "Assemble ${variantName} and publish the ${variantName} SDK to Maven Repository"
            dependsOn clean.name
            dependsOn unitTestTaskName
            dependsOn assembleVariantName
            dependsOn mavenPublicationTaskName

            tasks.named(assembleVariantName).get().dependsOn(unitTestTaskName)

            tasks.named(unitTestTaskName).get().mustRunAfter(clean)
            tasks.named(assembleVariantName).get().mustRunAfter(unitTestTaskName)
            tasks.named(mavenPublicationTaskName).get().mustRunAfter(assembleVariantName)
        }
    }
}